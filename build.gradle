import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'com.diffplug.spotless' version "6.22.0"
    id 'maven-publish'
}

group = 'com.aquatic'
version = System.getenv().getOrDefault("VERSION", "0")
description = 'Remote Write Client for Amazon Managed Prometheus'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.25.5'
    implementation 'org.xerial.snappy:snappy-java:1.1.10.7'

    implementation 'software.amazon.awssdk:auth:2.20.84'
    implementation 'software.amazon.awssdk:sts:2.20.84'

    testImplementation("org.junit.jupiter:junit-jupiter:5.9.2")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation("org.hamcrest:hamcrest:2.2")
}

publishing {
    publications {
        gpr(MavenPublication) {
            from(components.java)

            pom {
                name.set("amp_exporter_java") // Replace with your project name
                description.set("Amazon Managed Prometheus (AMP) Embedded Exporter for Java") // Replace with your project description
                url.set("https://github.com/aquanauts/amp_exporter_java") // Replace with your project URL

                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
            }
        }
    }


    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/aquanauts/amp_exporter_java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

test {
    reports.html.getRequired().set(false)
    reports.junitXml.getRequired().set(false)
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
        events TestLogEvent.FAILED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
    }
}

spotless {
    java {
        palantirJavaFormat("2.35.0")
        importOrder(
                'com.aquatic',
                '',
                'java',
                '\\#'
        )
        targetExclude("**/proto/**")
        removeUnusedImports()
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.3'
    }
}